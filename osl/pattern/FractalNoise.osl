/*
 *  Copyright 2023 LAIKA. Authored by Mitch Prater.
 *
 *  Licensed under the Apache License Version 2.0 http://apache.org/licenses/LICENSE-2.0,
 *  or the MIT license http://opensource.org/licenses/MIT, at your option.
 *
 *  This program may not be copied, modified, or distributed except according to those terms.
 */
shader pattern_FractalNoise
[[
    int rfm_nodeid = 1234265,
    string rfm_classification = "rendernode/RenderMan/pattern/Laika"
]]
(
    string Notes = ""
    [[
        int connectable = 0,
        string help =
            "Produces "
            "<a href='https://en.wikipedia.org/wiki/Fractional_Brownian_motion'>"
            "fractional Brownian motion</a> "
            "or a "
            "<a href='https://en.wikipedia.org/wiki/Fractal'> fractal</a> pattern "
            "using the selected <b>Noise Type</b>. "
    ]],

    point Space = point(0.0)
    [[
        string readOnly = "True",
        string help =
            "When connected, defines the <b>Space</b> "
            "in which the pattern is generated. "
            "<p>"
            "Otherwise, will use the <tt>__Pref</tt> primvar "
            "values if they exist, or object space if they do not. "
            "</p>"
    ]],

    float SpaceSize = 1.0
    [[
        string label = "Space Size",
        string readOnly = "True",
        string help =
            "The <b>Size</b> of the connected <b>Space</b>. "
    ]],

    float Size = 1.0
    [[
        int slider = 1, float slidermin = 0.1, float slidercenter = 1.0, float slidermax = 10.0,
        string help =
            "Controls the <b>Size</b> of the pattern variation. "
    ]],

    string NoiseType = "uperlin"
    [[
        int connectable = 0,
        string label = "Noise Type",
        string widget = "mapper",
        string options = "Perlin:uperlin|Simplex:usimplex|Cell:cell",
        string help =
            "Selects whether <i>Perlin</i>, <i>Simplex</i>, or <i>Cell</i> noise is used. "
    ]],

    int NoiseDim = 3
    [[
        int connectable = 0,
        string label = "Noise Dim",
        string widget = "mapper",
        string options = "2D:2|3D:3|4D:4",
        string help =
            "Explicitly controls the dimension of the <b>Noise</b>. "
            "For best results, use the lowest dimension for your pattern generation space. "
            "This can have a particularly large impact on the <i>Simplex</i> <b>Noise Type</b>. "
    ]],

    int FractalType = 0
    [[
        int connectable = 0,
        string label = "Fractal Type",
        string widget = "mapper",
        string options = "Fractal:0|fBm:1",
        string help =
            "Selects whether fractional Brownian motion (<i>fBm</i>) "
            "or a standard <i>Fractal</i> is produced. "
    ]],

    float Dimension = 1.0
    [[
        string label = "Dimension",
        int slider = 1, float slidermin = 0.5, float slidermax = 1.5,
        string conditionalVisPath = "../FractalType",
        string conditionalVisOp = "equalTo",
        string conditionalVisValue = "0",
        string help =
            "The result is a combination of <b>Iterations</b> number of patterns. "
            "<p>"
            "The amplitude of each successive iteration in the <i>Fractal</i> generation "
            "process is determined by the frequency (<tt>f</tt>) of the pattern "
            "and the <b>Dimension</b> value: "
            "</p>"
            "amplitude = <tt>1/(f^ß)</tt>, where <tt>ß = 3-2*</tt><b>Dimension</b>. "
            "<p>"
            "The process starts with the largest pattern with amplitude = 1. "
            "</p>"
    ]],

    float Gain = 0.5
    [[
        int slider = 1, float slidermin = 0.0, float slidermax = 1.0,
        string conditionalVisPath = "../FractalType",
        string conditionalVisOp = "equalTo",
        string conditionalVisValue = "1",
        string help =
            "The scaling factor used in <i>fBm</i> to control the contribution of each successive "
            "iteration. Bigger values are \"rougher\", smaller values are \"smoother\". "
            "The amplitude of each iteration is <b>Gain</b><tt>^(i-1)</tt>; where <tt>i</tt> is the "
            "iteration number. "
            "<p>"
            "The process starts with the largest pattern with amplitude = 1. "
            "</p>"
    ]],

    float Lacunarity = 2.0
    [[
        int connectable = 0,
        int slider = 1, float slidermin = 1.0, float slidermax = 4.0,
        string help =
            "The result is a combination of <b>Iterations</b> number of patterns. "
            "<p>"
            "The frequency of each successive pattern in the generation "
            "process is scaled up from the previous frequency by the <b>Lacunarity</b> value: "
            "<tt>f(i+1) = f(i)*</tt><b>Lacunarity</b>, where <tt>f</tt> is frequency "
            "and <tt>i</tt> is iteration number. "
            "</p>"
            "The process starts with the largest pattern at f = 1. "
    ]],

    int Iterations = 5
    [[
        int connectable = 0,
        int slider = 1, float slidermin = 1, float slidermax = 8, float sensitivity = 1,
        string help =
            "The result is a combination of <b>Iterations</b> number of patterns. "
            "<p>"
            "Each successive pattern in the generation process is "
            "<b>Lacunarity</b> times the frequency of the "
            "pattern generated before it. "
            "</p>"
            "The process starts with the largest pattern at "
            "f = 1 and amplitude = 1. "
    ]],

    float Contrast = 0.0
    [[
        string label = "Contrast",
        int slider = 1, float slidermin = 0.0, float slidermax = 1.0,
        string help =
            "Controls the amount of range expansion about 0.5 applied to the pattern, "
            "increasing the <b>Contrast</b> of each iteration. "
    ]],

    float Erosion = 0.0
    [[
        int slider = 1, float slidermin = -1.5, float slidermax = 1.5,
        string help =
            "<b>Erosion</b> determines the so-called \"crossover scale\" of the result. "
            "<p>"
            "<i>Negative</i> values will smooth out the <i>low valued</i> areas.<br/> "
            "<i>Positive</i> values will smooth the <i>high valued</i> ones.<br/> "
            "<i>0</i> gives a uniform appearance. "
            "</p>"
            "Implementation detail: the frequency exponent specified by the <b>Dimension</b> "
            "or <b>Gain</b> is itself offset as a function of the pattern's magnitude. "
            "<b>Erosion</b> adjusts that offset. "
    ]],

    float Distortion = 0.0
    [[
        int slider = 1, float slidermin = 0.0, float slidermax = 1.0,
        string help =
            "Controls what percentage the <b>Space</b> value is warped "
            "for succesive iterations during the pattern generation process. "
            "This distorion is based on the previous iteration's result, "
            "so this only has an effect when <b>Iterations</b> is 2 or more. "
    ]],

    // Organizationally, they ought to go right after Size,
    // but put them here since they're rarely used.
    float Variation = 0.0
    [[
        int slider = 1, float slidermin = 0.0, float slidermax = 10.0,
        string help =
            "Changes the resulting pattern without altering "
            "any of its fundamental characteristics. "
            "Can be used to animate the resulting pattern. "
    ]],

    int Periodic = 0
    [[
        int connectable = 0,
        string widget = "checkBox",
        string conditionalVisPath = "../NoiseType",
        string conditionalVisOp = "notEqualTo",
        string conditionalVisValue = "usimplex",
        string help =
            "Does the resulting pattern repeat or not? "
    ]],

    int SpacePeriod = 5
    [[
        int connectable = 0,
        string label = "Space Period",
        int slider = 1, int slidermin = 1, int slidermax = 20,
        string conditionalVisOp = "and",
        string conditionalVisLeft = "conditionalVis0",
        string conditionalVisRight = "conditionalVis1",
        string conditionalVis0Path = "../Periodic",
        string conditionalVis0Op = "equalTo",
        string conditionalVis0Value = "1",
        string conditionalVis1Path = "../NoiseType",
        string conditionalVis1Op = "notEqualTo",
        string conditionalVis1Value = "usimplex",
        string help =
            "At what <b>Space</b> dimension should the pattern repeat? "
            "This defines a cube in the <b>Space</b>, where all cubes "
            "of this <b>Space Period</b> size contain the same pattern, "
            "and the pattern tiles from one cube to the next. "
    ]],

    int VariationPeriod = 5
    [[
        int connectable = 0,
        string label = "Variation Period",
        string conditionalVisOp = "and",
        string conditionalVisLeft = "conditionalVis0",
        string conditionalVisRight = "conditionalVis1",
        string conditionalVis0Path = "../Periodic",
        string conditionalVis0Op = "equalTo",
        string conditionalVis0Value = "1",
        string conditionalVis1Path = "../NoiseType",
        string conditionalVis1Op = "notEqualTo",
        string conditionalVis1Value = "usimplex",
        int slider = 1, int slidermin = 1, int slidermax = 20,
        string help =
            "Sets the repetition interval for any <b>Variation.</b> "
    ]],

    output float  Float = 0.0,
    output color  Gray = color(0.0),
    output color  Color = color(0.0),
    output point  Point = point(0.0),
    output vector Vector = vector(0.0),
    output normal Normal = normal(0.0),
    output float  OutSize = 0.0
)
{
    // Pattern generation space.
    point  Q = Space;

    if( !isconnected( Space ))
    {
        Q = P; // Referencing P is required for any down-stream auto-differentiation.
        getattribute( "builtin", "Po", Q ); // Use un-displaced P.
        getattribute( "primvar", "__Pref", Q ); // Use __Pref if present.
        Q = transform( "common", "object", Q ); // Correct in all cases.
    }

    // Size scaling.
    float  size = max( 1.0e-6, Size );
    OutSize = SpaceSize * size;
    Q /= size;

    // Initialize the iteration variables.
    color  sum = color(0.0);
    color  mag = color(1.0);
    float  freq = 1.0;

    Color = color(0.0);

    // Compute the fractal.
    for( int i=0; i < Iterations; i++ )
    {
        // This iteration's pattern.
        color  iP;

        // Compute this iteration's pattern.
        if( 1 == Periodic && "usimplex" != NoiseType )
        {
            // This iteration's periodic interval.
            float  interval = round( freq*SpacePeriod );

            // This iteration's periodic space.
            point  iQ = Q*interval / SpacePeriod;

            // This iteration's spatial offset.
            float  iOffset = ( i*Lacunarity + Variation )*interval / SpacePeriod;

            if( 2 == NoiseDim )
            {
                iP = pnoise( NoiseType, iQ.x + iOffset, iQ.y + iOffset, SpacePeriod, SpacePeriod );
            }
            else if( 3 == NoiseDim )
            {
                iP = pnoise( NoiseType, iQ + vector(iOffset), SpacePeriod );
            }
            else // 4 == NoiseDim
            {
                iP = pnoise( NoiseType, iQ, iOffset, SpacePeriod, VariationPeriod );
            }
        }
        else
        {
            // This iteration's space.
            point  iQ = Q*freq;

            // This iteration's spatial offset.
            float  iOffset = i*Lacunarity + Variation;

            if( 2 == NoiseDim )
            {
                iP = noise( NoiseType, iQ.x + iOffset, iQ.y + iOffset );
            }
            else if( 3 == NoiseDim )
            {
                iP = noise( NoiseType, iQ + vector(iOffset) );
            }
            else // 4 == NoiseDim
            {
                iP = noise( NoiseType, iQ, iOffset );
            }
        }

        // Contrast expand the pattern.
        float  eps = 0.5 * linearstep( 0.2, 1.0, Contrast );
        iP = mix( iP, smoothstep( eps, 1.0-eps, iP ), linearstep( 0.0, 1.0, 5.0*Contrast ));

        // Add this iteration's contribution to the result.
        Color += mag * iP;

        // Accumulate this iteration's magnitude.
        sum += mag;

        // Update variables for the next iteration.
        // Erosion magnitude adjustment (based on normalized current result).
        color  erosion = mix( color(Erosion), -color(Erosion), Color/sum );

        if( 0 == FractalType )
        {
            color dim = color(Dimension) + erosion;
            dim = clamp( dim, color(0), color(2) );
            mag = color(1) / pow( freq, color(3) - 2*dim );
        }
        else
        {
            color gain = color(Gain) + erosion;
            gain = clamp( gain, color(0.01), color(0.99) );
            mag = pow( gain, i );
        }

        Q += mag * Distortion*vector( iP - color(0.5) );
        freq *= Lacunarity;
    }
    // End fractal computation.

    // Normalize the result.
    Color /= sum;

    // Set the other outputs.
    Float = Color.r;
    Gray = color( Float );
    Point = point( Color );
    Vector = vector( 2.0*Color - color(1.0) );
    Normal = normalize( Vector );
}
